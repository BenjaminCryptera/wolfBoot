FLASH_SIZE = @BOOTLOADER_PARTITION_SIZE@;
FLASH_START = 0x100000000 - @BOOTLOADER_PARTITION_SIZE@;
BOOTLOADER_JUMP32_START = 0xfffff000;
RESETVECTOR_START = 0xffffffec;
FSP_T_ORIGIN = @FSP_T_BASE@; /* default base:size 0xFFFFF000:0x3000 [0xfffff000:0x100002000] */
FSP_M_ORIGIN = @FSP_M_BASE@; /* default base:size 0xfffdd000:0x22000 [0xfffdd000:0xfffff000] */
FSP_S_ORIGIN = @FSP_S_BASE@; /* default base:size 0xfffc8000:0x15000 [0xfffdd000:0xfffdd000] */
FIT_TABLE_PTR = 0xffffffc0;
UCODE0_BASE = @UCODE0_BASE@;
FSP_S_UPD_DATA_BASE = @FSP_S_UPD_DATA_BASE@;
WOLFBOOT_ORIGIN = @WOLFBOOT_ORIGIN@;
OUTPUT_FORMAT(elf32-i386)

MEMORY
{
    FLASH (rx) : ORIGIN = FLASH_START LENGTH = FLASH_SIZE
    RAM (rwx) : ORIGIN = 0x0 LENGTH = FLASH_START
}

SECTIONS
{
    .jmpto32 BOOTLOADER_JUMP32_START :
    {
       _off_boot = ABSOLUTE(.) & 0xffff;
       KEEP(*(.jmpto32))
    }

    .fit_table_tr FIT_TABLE_PTR :
    {
        QUAD(fit_table);
    }

    .reset_vector RESETVECTOR_START :
    {
        KEEP(*(.reset_vector))
    }

    .ucode_update0 UCODE0_BASE :
    {
        *(.ucode0)
    }

    .bootloader WOLFBOOT_ORIGIN :
    {
       KEEP(./boot_x86_fsp_start.o(.boot*))
       KEEP(./tgl_fsp.o(.boot))
       *(.boot*)
       *(.text*)
       *(.rodata*)
       *(.eh_frame*)
       *(.data*)
       . = ALIGN(4);
    }

    .fsps_upd FSP_S_UPD_DATA_BASE :
    {
    	KEEP(./fsp_tgl_s_upd.o(.fsps_upd))
    }

   .text FLASH_START :
    {
        _wolfboot_flash_start = .;
        KEEP(*(.sig_wolfboot_raw*))
        *(.wolfboot)
        _wolfboot_flash_end = .;
    }

    .fsp_t FSP_T_ORIGIN :
    AT(FSP_T_ORIGIN)
    {
       _start_fsp_t = .;
       *(.fsp_t)
    }

    .fsp_s FSP_S_ORIGIN :
    {
       _fsp_s_hdr = .;
       KEEP(*(.sig_fsp_s*))
       _start_fsp_s = .;
       *(.fsp_s)
       _end_fsp_s = .;
    }

    .fsp_m FSP_M_ORIGIN :
    {
       _fsp_m_hdr = .;
       _start_fsp_m = .;
       *(.fsp_m)
       _end_fsp_m = .;
    }

}
